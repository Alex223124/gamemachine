apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'io.gamemachine'
archivesBaseName = "common"
 version = '1.1.0-SNAPSHOT'


description = """"""

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
  mavenCentral()
  maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
  
}


dependencies {
  compile 'org.slf4j:slf4j-api:1.7.7'
  compile 'io.protostuff:protostuff:1.1.0'
  compile 'io.protostuff:protostuff-api:1.1.0'
  compile 'io.protostuff:protostuff-core:1.1.0'
  compile 'io.protostuff:protostuff-runtime:1.1.0'
  compile 'io.protostuff:protostuff-json:1.1.0'
  
  compile group: 'com.typesafe.akka', name: "akka-actor_$scala_version", version:"$akka_version"
  compile group: 'com.typesafe.akka', name: "akka-kernel_$scala_version", version:"$akka_version"
  compile 'com.fasterxml.jackson.core:jackson-databind:2.4.3'
  compile 'com.google.guava:guava:18.0'
  compile 'org.apache.httpcomponents:httpclient:4.3.5'
  compile 'io.netty:netty-all:4.0.24.Final'
  compile "com.badlogicgames.gdx:gdx-ai:1.4.1-SNAPSHOT"
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.1'
}

task install_libs(type: Copy) {
  into "$buildDir/../lib"
    
  from configurations.runtime
  from "$buildDir/libs"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Game Machine commons'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'A collection of common libraries used by Game Machine core.'
        url 'http://www.gamemachine.io'

        scm {
          connection 'scm:git:git://github.com/gamemachine/gamemachine'
          developerConnection 'scm:git:git@github.com:gamemachine/gamemachine.git'
          url 'https://github.com/gamemachine/gamemachine'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            name 'Chris Ochs'
            email 'chris@gamemachine.io'
          }
        }
      }
    }
  }
}

signing {
    sign configurations.archives
}

//task removeOldSnapshots(type: Exec) {
//  executable "sh"
//  args "-c", "cd /home/chris/maven-repo;git rm -rf io/gamemachine/common/1.1.0-SNAPSHOT;git commit -am 'remove last snapshot';git push origin master"
//}


//task publish_to_github(type: Exec, dependsOn: publish) {
//  executable "sh"
//  args "-c", "cd /home/chris/maven-repo;git add io/*;git commit -am 'release';git push origin master"
//}
//publish.dependsOn removeOldSnapshots
//publish.mustRunAfter removeOldSnapshots

